/mob/living/carbon/human/species/goblin/npc/conjured/champion
	gob_outfit = /datum/outfit/job/roguetown/conjured_goblin_champion
	d_intent = INTENT_PARRY
	dodgetime = 25
	var/loadout = "brute"

/mob/living/carbon/human/species/goblin/npc/conjured/champion/after_creation()
	..()
	name = "phantasmal goblin champion"
	real_name = "phantasmal goblin champion"
	SEND_SIGNAL(src, COMSIG_MOB_MODIFY_AGGRO_LINES, GLOB.large_goblin_aggro, TRUE)
	ADD_TRAIT(src, TRAIT_BIGGUY, TRAIT_GENERIC)
	ADD_TRAIT(src, TRAIT_HEAVYARMOR, TRAIT_GENERIC)
	ADD_TRAIT(src, TRAIT_NODISMEMBER, TRAIT_GENERIC)
	ADD_TRAIT(src, TRAIT_BADTRAINER, TRAIT_GENERIC)
	REMOVE_TRAIT(src, TRAIT_CRITICAL_WEAKNESS, null)
	src.transform = src.transform.Scale(1.25, 1.25)
	src.pixel_y += round(0.25 * 16)

/datum/outfit/job/roguetown/conjured_goblin_champion/proc/goblin_tier(mob/living/carbon/human/H)
	if(istype(H, /mob/living/carbon/human/species/goblin/npc/conjured/champion))
		var/mob/living/carbon/human/species/goblin/npc/conjured/champion/G = H
		return G.gear_tier
	return 1

/datum/outfit/job/roguetown/conjured_goblin_champion/pre_equip(mob/living/carbon/human/H, visualsOnly)
	. = ..()
	var/loadout = "brute"
	var/tier = goblin_tier(H)
	if(istype(H, /mob/living/carbon/human/species/goblin/npc/conjured/champion))
		var/mob/living/carbon/human/species/goblin/npc/conjured/champion/G = H
		loadout = G.loadout
	var/skill = clamp(tier * 2, 2, 4)
	H.STASTR = 11 + tier
	H.STASPD = 11
	H.STACON = 11 + (tier * 2)
	H.STAWIL = 10 + (tier * 2)
	H.STAPER = 6
	H.STAINT = 8
	H.STALUC = 4
	H.adjust_skillrank(/datum/skill/combat/maces, skill, TRUE)
	H.adjust_skillrank(/datum/skill/combat/axes, skill, TRUE)
	H.adjust_skillrank(/datum/skill/combat/whipsflails, skill, TRUE)
	H.adjust_skillrank(/datum/skill/combat/wrestling, skill, TRUE)
	H.adjust_skillrank(/datum/skill/combat/unarmed, skill, TRUE)
	H.adjust_skillrank(/datum/skill/combat/shields, max(skill - 1, 2), TRUE)
	if(tier >= 2)
		armor = /obj/item/clothing/suit/roguetown/armor/plate/cuirass/iron/goblin
		head = /obj/item/clothing/head/roguetown/helmet/goblin
	else
		armor = /obj/item/clothing/suit/roguetown/armor/leather/hide/goblin
		head = /obj/item/clothing/head/roguetown/helmet/leather/goblin
	shoes = /obj/item/clothing/shoes/roguetown/boots/leather
	gloves = /obj/item/clothing/gloves/roguetown/leather
	switch(loadout)
		if("berserker")
			r_hand = (tier == 3) ? /obj/item/rogueweapon/greataxe/steel : /obj/item/rogueweapon/greataxe/militia
		if("flailman")
			r_hand = /obj/item/rogueweapon/flail
			l_hand = (tier >= 2) ? /obj/item/rogueweapon/shield/tower/metal : /obj/item/rogueweapon/shield/wood
		if("bomber")
			r_hand = /obj/item/rogueweapon/mace
			neck = /obj/item/storage/belt/rogue/pouch/bombs/conjured
		else
			r_hand = /obj/item/rogueweapon/mace
			l_hand = (tier >= 2) ? /obj/item/rogueweapon/shield/tower/metal : /obj/item/rogueweapon/shield/wood
